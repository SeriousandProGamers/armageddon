buildscript {
	repositories {
		maven { url = 'https://files.minecraftforge.net/maven' }
		jcenter()
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
	}
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = "${mod_version}"
group = "${apex_package}.${mod_id}"
archivesBaseName = "${mod_name}-${mc_version}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

sourceSets {
	api
	main
}

configurations {
	apiCompile.extendsFrom(compile)
}

minecraft {
	mappings channel: 'snapshot', version: "${mappings_version}"
	// makeObfSourceJar = false
	// accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

	runs {
		client {
			workingDirectory project.file('run/client')

			// property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			// property 'forge.logging.console.level', 'debug'

			mods {
				Armageddon {
					source sourceSets.main
					source sourceSets.api
				}
			}
		}

		clientAuthed {
			parent runs.client

			if(project.hasProperty('mc_uuid')) {
				args '--uuid', project.getProperty('mc_uuid');
			}

			if(project.hasProperty('mc_username')) {
				args '--username', project.getProperty('mc_username');
			}

			if(project.hasProperty('mc_accessToken')) {
				args '--accessToken', project.getProperty('mc_accessToken');
			}
		}

		server {
			workingDirectory project.file('run/server')

			// property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			// property 'forge.logging.console.level', 'debug'

			args 'nogui'

			mods {
				Armageddon {
					source sourceSets.main
					source sourceSets.api
				}
			}
		}

		data {
			workingDirectory project.file('run/data')

			// property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			// property 'forge.logging.console.level', 'debug'

			args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

			mods {
				Armageddon {
					source sourceSets.main
					source sourceSets.api
				}
			}
		}
	}
}

sourceSets.main.resources {
	srcDir 'src/generated/resources'
}

repositories {
	mavenLocal()
	maven { url 'https://dvs1.progwml6.com/files/maven' } // JEI
	maven { url 'https://maven.tterrag.com/' } // TOP
}

dependencies {
	minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

	implementation sourceSets.api.output

	// JEI
	compileOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}:api")
	runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}")

	// TOP
	compileOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-${mc_version_short}:${mc_version_short}-${top_version}:api")
	runtimeOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-${mc_version_short}:${mc_version_short}-${top_version}")

	// Local Deps
	if(project.hasProperty('apex_local_deps') && project.getProperty('apex_local_deps')) {
		runtimeOnly fg.deobf("com.apex.forge.devworld:devworld:${mc_version}-${devworld_version}")
	}
}

task apiJar(type: Jar, dependsOn: classes) {
	classifier = 'api'
	// from sourceSets.api.output
	from sourceSets.api.allJava
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allJava
	from sourceSets.api.allJava
}

task deobfJar(type: Jar) {
	classifier = 'deobf'
	from sourceSets.main.output
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives jar
	archives javadocJar
	archives sourcesJar
	archives deobfJar
	archives apiJar
}

jar {
	from sourceSets.main.output.classesDirs
	from sourceSets.api.output.classesDirs
	from sourceSets.main.output.resourcesDir
	from sourceSets.api.output.resourcesDir

	manifest {
		attributes([
				'Specification-Title': "${mod_name}",
				'Specification-Vendor': "${mod_author}",
				'Specification-Version': '1',
				'Implementation-Title': "${mod_name}",
				'Implementation-Version': "${mod_version}",
				'Implementation-Vendor' : "${mod_name}",
				'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

jar.finalizedBy('reobfJar')

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = "${apex_package}"
			artifactId = "${mod_id}"
			version = "${mc_version}-${mod_version}"

			artifact jar
			artifact javadocJar
			artifact sourcesJar
			artifact deobfJar
			artifact apiJar
		}
	}
	repositories {
		mavenLocal()
	}
}